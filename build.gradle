buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
        maven { url 'http://dl.bintray.com/jfrog/jfrog-jars' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'bintray'
apply plugin: 'artifactory'

group = 'org.hogel'
version = '0.0.6-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'org.slf4j:slf4j-api:1.7.7'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.google.guava:guava:17.0'
    testCompile 'ch.qos.logback:logback-classic:1.1.2'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "$group"
            artifactId "bookscan-client"
            version "$version"

            from components.java
            artifact sourceJar
            artifact javadocJar
        }

        repositories {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}


artifactory {
  contextUrl = 'http://oss.jfrog.org'
}

artifactoryPublish { task ->
  artifactory {
    publish {
      repository {
        repoKey = 'oss-snapshot-local'

        // Need this to enable non deployment builds without credentials
        gradle.taskGraph.whenReady { taskGraph ->
          if (taskGraph.hasTask(task)) {
            username = "$BINTRAY_USER"
            password = "$BINTRAY_TOKEN"
          }
        }
      }
    }
  }
}

bintray {
    user = "$BINTRAY_USER"
    key = "$BINTRAY_TOKEN"
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = 'hogelog'
        name = 'bookscan-client-java'
        desc = 'Bookscan client for Java'
        licenses = ['MIT']
        labels = []
    }
}
bintrayUpload.dependsOn publish

task releaseTag(type:Exec) {
    commandLine 'git', 'tag', "$version"
}

task releasePush(type:Exec) {
    commandLine 'git', 'push', "--tags"
}

task release(dependsOn: [check, publish, releaseTag, releasePush, bintrayUpload])
publish.mustRunAfter check
releaseTag.mustRunAfter publish
releasePush.mustRunAfter releaseTag
bintrayUpload.mustRunAfter releasePush
