buildscript {
    repositories {
        maven { url 'http://jcenter.bintray.com' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.3'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'bintray'

group = 'org.hogel'
version = '0.0.3'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'

    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'com.google.guava:guava:17.0'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId "$group"
            artifactId "bookscan-client"
            version "$version"

            from components.java
            artifact sourceJar
            artifact javadocJar
        }

        repositories {
            maven {
                url "$buildDir/repo"
            }
        }
    }
}

bintray {
    user = "$BINTRAY_USER"
    key = "$BINTRAY_TOKEN"
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = 'hogelog'
        name = 'bookscan-client-java'
        desc = 'Bookscan client for Java'
        licenses = ['MIT']
        labels = []
    }
}
bintrayUpload.dependsOn publish

task releaseTag(type:Exec) {
    commandLine 'git', 'tag', "$version"
}

task releasePush(type:Exec) {
    commandLine 'git', 'push', "--tags"
}

task release(dependsOn: [check, publish, releaseTag, releasePush, bintrayUpload])
publish.mustRunAfter check
releaseTag.mustRunAfter publish
releasePush.mustRunAfter releaseTag
bintrayUpload.mustRunAfter releasePush
